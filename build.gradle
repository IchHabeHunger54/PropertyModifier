//file:noinspection GroovyAssignabilityCheck
import groovy.json.JsonSlurper
import groovy.json.JsonOutput

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.codehaus.groovy', name: 'groovy-all', version: '3.0.8', ext: 'pom'
        classpath group: 'org.apache.maven', name: 'maven-plugin-api', version: '3.8.1'
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: "${project.forgegradle_version}", changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: "${project.mixingradle_version}", changing: true
        classpath group: 'org.parchmentmc', name: 'librarian', version: '1.+'
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
    maven { //JEI
        name = 'ModMaven'
        url = 'https://modmaven.dev'
    }
    maven { //MixinExtras
        name = 'Jitpack'
        url = 'https://jitpack.io'
    }
    maven { //Testing
        name = 'Curse Maven'
        url 'https://cursemaven.com'
    }
    maven { //Quark
        name = 'BlameJared Maven'
        url = 'https://maven.blamejared.com'
    }
}

group = "${project.mod_group}"
version = "${project.mc_version}-${project.mod_version}"
archivesBaseName = "${project.mod_id}"
java.toolchain.languageVersion = JavaLanguageVersion.of(project.java_version)

configurations {
    include { transitive = false }
    library
    shade
    implementation.extendsFrom include
    implementation.extendsFrom library
    implementation.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${project.mc_version}-${project.forge_version}"
    shade fg.deobf("com.github.LlamaLad7:MixinExtras:${project.mixinextras_version}")
    library fg.deobf("com.github.LlamaLad7:MixinExtras:${project.mixinextras_version}")
    annotationProcessor "com.github.LlamaLad7:MixinExtras:${project.mixinextras_version}"
    annotationProcessor "org.spongepowered:mixin:${project.mixin_version}:processor"
    compileOnly fg.deobf("mezz.jei:jei-${project.mc_version}-forge:${project.jei_version}")
    runtimeOnly fg.deobf("mezz.jei:jei-${project.mc_version}-forge:${project.jei_version}")
    runtimeOnly fg.deobf("vazkii.autoreglib:AutoRegLib:${project.autoreglib_version}")
    runtimeOnly fg.deobf("vazkii.quark:Quark:${project.quark_version}")
}

minecraft {
    mappings channel: "${project.mappings_channel}", version: "${project.mappings_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=${project.mod_id}.mixins.json"
            mods {
                propertymodifier {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=${project.mod_id}.mixins.json"
            mods {
                propertymodifier {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            property 'forge.logging.console.level', 'debug'
            arg "-mixin.config=${project.mod_id}.mixins.json"
            args '--mod', "${project.mod_id}", '--all', '--output', file('src/main/generated/'), '--existing', file('src/main/resources/')
            mods {
                propertymodifier {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/main/generated' }

minecraft.runs.all {
    lazyToken('minecraft_classpath') {
        configurations.library.copyRecursive().resolve().collect { it.absolutePath }.join(File.pathSeparator)
    }
}

processResources {
    duplicatesStrategy DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'META-INF/mods.toml'
        expand 'mod_id': mod_id, 'mod_name': mod_name,
                'mod_version': mod_version, 'mod_url': mod_url,
                'mod_updates': mod_updates,
                'mod_authors': mod_authors,
                'mod_credits': mod_credits,
                'mod_description': mod_description,
                'mod_logo': mod_logo,
                'mc_version_range': mc_version_range,
                'forge_version_range': forge_version_range,
                'loader_version_range': loader_version_range
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'META-INF/mods.toml'
    }
    doLast {
        fileTree(dir: outputs.files.asPath, include: "**/*.json").each {
            it.text = JsonOutput.toJson(new JsonSlurper().parse(it))
        }
    }
}

jar {
    manifest {
        mainAttributes(
                "Maven-Artifact": "${project.mod_group}:${project.archivesBaseName}:${project.version}",
                "Specification-Title": archivesBaseName,
                "Specification-Vendor": "${project.mod_authors}",
                "Specification-Version": "1",
                "Implementation-Title": "${archivesBaseName}",
                "Implementation-Version": "${project.mod_version}",
                "Implementation-Vendor": "${project.mod_authors}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "Built-On-Java": "${System.getProperty('java.vm.version')} (${System.getProperty('java.vm.vendor')})",
                "Built-On": "${project.mc_version}-${project.forge_version}"
        )
    }
}
jar.finalizedBy('reobfJar')

shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'com.llamalad7.mixinextras', "${project.group}.mixinextras"
    mergeServiceFiles()
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}

mixin {
    add sourceSets.main, "${project.mod_id}.refmap.json"
    config "${project.mod_id}.mixins.json"
}
